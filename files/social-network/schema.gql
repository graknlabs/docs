define

################
## ATTRIBUTES ##
################
	name sub attribute, value string;
	full-name sub attribute, value string;
	nickname sub attribute, value string;

	title sub attribute,
		value string;

	event-date sub attribute,
		abstract,
		value datetime;
	approved-date sub event-date;
	birth-date sub event-date;
	start-date sub event-date;
	end-date sub event-date;
	engagement-date sub event-date;

	published-date sub attribute,
		value datetime;

	reference-id sub attribute,
		value string;

	salary sub attribute,
		value long;

	registration-number sub attribute,
		value string;

	identifier sub attribute,
    		value string;

	graduated sub attribute,
		value boolean;

	score sub attribute,
		value double;

	ranking sub attribute,
		value long;

	phone-number sub attribute,
		value string;

	email sub attribute,
		value string;

	gender sub attribute,
		value string;

	engaged sub attribute,
		value boolean;
	separated sub attribute,
		value boolean;
	divorced sub attribute,
		value boolean;

	file sub attribute,
		value string;

	caption sub attribute,
		value string;

	language sub attribute,
		value string,
		plays speaking-of-language:spoken,
		plays speaking-language-mutuality:mutual-language;

	content sub attribute,
		value string,
		owns language;

	emotion sub attribute,
		value string,
		regex "^(like|love|funny|shocking|sad|angry)$",
		plays reaction:reacted-emotion;

	link sub attribute,
		value string,
		plays attachment:attached;

	person sub entity,
		owns full-name,
		owns nickname,
		owns gender,
		owns phone-number,
		owns email @key;

###################
## GENERIC TYPES ##
###################

	location-of-everything sub relation,
		abstract,
		relates located-subject,
		relates subject-location;

	location-hierarchy sub relation,
		relates subordinate-location,
		relates superior-location;

	ownership sub relation,
		relates owner,
		relates owned;

	location sub entity,
		owns name @key,
		plays location-hierarchy:subordinate-location,
		plays location-hierarchy:superior-location;

	request sub relation,
		relates approved-subject,
		relates requester,
		relates respondent;

	periodic-event sub relation,
		abstract,
		owns start-date,
		owns end-date,
		plays event-overlapping:overlapped-event;

	########################
	## events overlapping ##
	########################

	event-overlapping sub relation,
		relates overlapped-event;

	rule events-overlap:
		when {
			$e1 isa periodic-event;
			$e1 has start-date $sd1, has end-date $ed1;
			$e2 isa periodic-event;
			$e2 has start-date $sd2, has end-date $ed2;
			$sd2 > $sd1;
			$sd2 < $ed1;
			$e1 != $e2;
		} then {
			(overlapped-event: $e1, overlapped-event: $e2) isa event-overlapping;
		};

###########
## BIRTH ##
###########

## query the schema section
# match
# 	$per sub person;
# 	$lob sub location-of-birth;
# 	$bir sub birth;
# 	$loc sub location;
# 	$blm sub birth-location-mutuality;
# get;

	location-of-birth sub location-of-everything,
		relates located-birth as located-subject,
		relates birth-location as subject-location;

	birth sub relation,
		owns birth-date,
		relates birthed-child,
		plays location-of-birth:located-birth,
		plays birth-mutuality:mutual-birth;

	person sub entity,
		plays birth:birthed-child,
		plays birth-mutuality:mutual-birthed-child;

	location sub entity,
		plays location-of-birth:birth-location,
		plays birth-mutuality:mutual-birth-location;

	##############################
	## birth location in common ##
	##############################

	birth-mutuality sub relation,
		relates mutual-birthed-child,
		relates mutual-birth-location,
		relates mutual-birth;

	rule people-born-at-the-same-location:
		when {
			$b1 (birthed-child: $p1) isa birth;
			$b2 (birthed-child: $p2) isa birth;
			($b1, birth-location: $l) isa location-of-birth;
			($b2, birth-location: $l) isa location-of-birth;
			$p1 != $p2;
		} then {
			(mutual-birthed-child: $p1, mutual-birthed-child: $p2, mutual-birth-location: $l, mutual-birth: $b1, mutual-birth: $b2) isa birth-mutuality;
		};

################
## PARENTSHIP ##
################

	person sub entity,
		plays parentship:parent,
		plays parentship:father,
		plays parentship:mother,
		plays parentship:child,
		plays parentship:son,
		plays parentship:daughter;

    parentship sub relation,
  		relates parent,
		relates child,
		relates father,
		relates mother,
		relates son,
		relates daughter;

	person-with-a-mother sub entity;
	person-with-a-father sub entity;

	rule genderizeParentships1:
		when {
			(parent: $p, child: $c) isa parentship;
			$p has gender 'male';
			$c has gender 'male';
		} then {
			(father: $p, son: $c) isa parentship;
		};

	rule genderizeParentships2:
		when {
			(parent: $p, child: $c) isa parentship;
			$p has gender 'male';
			$c has gender 'female';
		} then {
			(father: $p, daughter: $c) isa parentship;
		};

	rule genderizeParentships3:
		when {
			(parent: $p, child: $c) isa parentship;
			$p has gender 'female';
			$c has gender 'male';
		} then {
			(mother: $p, son: $c) isa parentship;
		};

	rule genderizeParentships4:
		when {
			(parent: $p, child: $c) isa parentship;
			$p has gender 'female';
			$c has gender 'female';
		} then {
			(mother: $p, daughter: $c) isa parentship;
		};

###############
## RESIDENCY ##
###############

## query the schema section
# match
# 	$per sub person;
# 	$lor sub location-of-residence;
# 	$res sub residency;
# 	$loc sub location;
# get;

	location-of-residence sub location-of-everything,
		relates located-residence as located-subject,
		relates residence as subject-location;

	residency sub periodic-event,
		relates resident,
		plays location-of-residence:located-residence,
		plays residence-mutuality:mutual-residency;

	person sub entity,
		plays residency:resident,
		plays residence-mutuality:mutual-resident;

	location sub entity,
		plays location-of-residence:residence,
		plays residence-mutuality:mutual-residence;

	#########################
	## residence in common ##
	#########################

	residence-mutuality sub relation,
		relates mutual-resident,
		relates mutual-residency,
		relates mutual-residence;

	rule people-resided-at-the-same-location:
		when {
			$r1 (resident: $p1) isa residency;
			$r2 (resident: $p2) isa residency;
			($r1, residence: $l) isa location-of-residence;
			($r2, residence: $l) isa location-of-residence;
			$p1 != $p2;
		} then {
			(mutual-resident: $p1, mutual-resident: $p2, mutual-residence: $l, mutual-residency: $r1, mutual-residency: $r2) isa residence-mutuality;
		};

###############
## EDUCATION ##
###############

## query the schema section
# match
# 	$sco sub school-course-offering;
# 	$los sub location-of-school;
# 	$sce sub school-course-enrollment;
# 	$sch sub school;
# 	$scc sub school-course;
# 	$per sub person;
# 	$loc sub location;
# get;

	location-of-school sub location-of-everything,
		relates located-school as located-subject,
		relates school-location as subject-location;

	school-course-enrollment sub periodic-event,
		owns graduated,
		owns score,
		relates student,
		relates enrolled-course,
		relates enrolling-school,
		plays course-enrollment-mutuality:mutual-course-enrollment;

	school sub entity,
		owns name @key,
		owns ranking,
		plays school-course-offering:offering-school,
		plays location-of-school:located-school,
		plays school-course-enrollment:enrolling-school,
		plays school-mutuality:mutual-school;

	school-course sub entity,
		owns title @key,
		plays school-course-offering:offered-course,
		plays school-course-enrollment:enrolled-course;

	person sub entity,
		plays school-course-enrollment:student,
		plays school-mutuality:schoolmate,
		plays course-enrollment-mutuality:coursemate;

	location sub entity,
		plays location-of-school:school-location;

	##########################
	## school offerings ##
	##########################

	school-course-offering sub relation,
		relates offering-school,
		relates offered-course;

	rule school-offers-the-enrolled-courses:
		when {
			(student: $p, enrolled-course: $c, enrolling-school: $s) isa school-course-enrollment;
		} then {
			(offering-school: $s, offered-course: $c) isa school-course-offering;
		};

	##########################
	## educations in common ##
	##########################

	school-mutuality sub relation,
		relates schoolmate,
		relates mutual-school;

	rule people-gone-to-the-same-school:
		when {
			(student: $p1, enrolled-course: $c1) isa school-course-enrollment;
			(student: $p2, enrolled-course: $c2) isa school-course-enrollment;
			(offered-course: $c1, offering-school: $s) isa school-course-offering;
			(offered-course: $c2, offering-school: $s) isa school-course-offering;
			$p1 != $p2;
		} then {
			(schoolmate: $p1, schoolmate: $p2, mutual-school: $s) isa school-mutuality;
		};

	course-enrollment-mutuality sub relation,
		relates coursemate,
		relates mutual-course-enrollment;

	rule people-taken-the-same-course:
		when {
			$sce1 (student: $p1, enrolled-course: $sc) isa school-course-enrollment;
			$sce2 (student: $p2, enrolled-course: $sc) isa school-course-enrollment;
			$p1 != $p2;
		} then {
			(coursemate: $p1, coursemate: $p2, mutual-course-enrollment: $sce1, mutual-course-enrollment: $sce2) isa course-enrollment-mutuality;
		};

############
## TRAVEL ##
############

## query the schema section
# match
# 	$lot sub location-of-travel;
# 	$tra sub travel;
# 	$per sub person;
# 	$loc sub location;
# get;

	location-of-travel sub location-of-everything,
		relates located-travel as located-subject,
		relates travel-location as subject-location;

	travel sub periodic-event,
		relates traveler,
		plays location-of-travel:located-travel,
		plays travel-mutuality:mutual-travel;

	person sub entity,
		plays travel:traveler,
		plays travel-mutuality:mutual-traveler;

	location sub entity,
		plays location-of-travel:travel-location,
		plays travel-mutuality:mutual-travel-location;

	#######################
	## travels in common ##
	#######################

	travel-mutuality sub relation,
		relates mutual-traveler,
		relates mutual-travel,
		relates mutual-travel-location;

	rule people-traveled-to-the-same-location:
		when {
			$t1 (traveler: $p1) isa travel;
			$t2 (traveler: $p2) isa travel;
			($t1, travel-location: $l) isa location-of-travel;
			($t2, travel-location: $l) isa location-of-travel;
			$p1 != $p2;
		} then {
			(mutual-traveler: $p1, mutual-traveler: $p2, mutual-travel-location: $l, mutual-travel: $t1, mutual-travel: $t2) isa travel-mutuality;
		};

##########
## WORK ##
##########

## query the schema section
# match
# 	$oow sub office-ownership;
# 	$emp sub employment;
# 	$loo sub location-of-office;
# 	$wpo sub work-position;
# 	$org sub organisation;
# 	$off sub office;
# 	$loc sub location;
# 	$per sub person;
# 	$emm sub employment-mutuality;
# 	$wpm sub work-position-mutuality;
# get;

	employment sub periodic-event,
		owns reference-id @key,
		owns salary,
		relates employer,
		relates employee,
		relates offered-position,
		plays employment-mutuality:mutual-employment;

	location-of-office sub location-of-everything,
		relates located-office as located-subject,
		relates office-location as subject-location;

	office-ownership sub ownership,
		relates office-owner as owner,
		relates owned-office as owned;

	work-position sub entity,
		owns title @key,
		plays employment:offered-position,
		plays work-position-mutuality:mutual-position;

	organisation sub entity,
		owns name @key,
		owns registration-number @key,
		plays office-ownership:office-owner,
		plays employment:employer,
		plays employment-mutuality:mutual-organisation;

	office sub entity,
	    owns registration-number @key,
		plays office-ownership:owned-office,
		plays location-of-office:located-office;

	location sub entity,
		plays location-of-office:office-location;

	person sub entity,
		plays employment:employee,
		plays employment-mutuality:mutual-employee;

	##########################
	## employment in common ##
	##########################

	employment-mutuality sub relation,
		relates mutual-employee,
		relates mutual-employment,
		relates mutual-organisation;

	rule people-work-at-the-same-organisation:
		when {
			$e1 (employee: $p1, employer: $o) isa employment;
			$e2 (employee: $p2, employer: $o) isa employment;
			$p1 != $p2;
		} then {
			(mutual-employee: $p1, mutual-employee: $p2, mutual-organisation: $o, mutual-employment: $e1, mutual-employment: $e2) isa employment-mutuality;
		};

	work-position-mutuality sub relation,
		relates mutual-employee,
		relates mutual-employment,
		relates mutual-position;

	rule people-work-at-the-same-position:
		when {
			$e1 (employee: $p1, offered-position: $p) isa employment;
			$e2 (employee: $p2, offered-position: $p) isa employment;
			$p1 != $p2;
		} then {
			(mutual-employee: $p1, mutual-employee: $p2, mutual-position: $p, mutual-employment: $e1, mutual-employment: $e2) isa work-position-mutuality;
		};

##############
## Language ##
##############

## query the schema section
# match
# 	$sol sub speaking-of-language;
# 	$per sub person;
# 	$lan sub language;
# 	$slm sub speaking-language-mutuality;
# get;

	speaking-of-language sub relation,
		relates speaker,
		relates spoken,
		plays speaking-language-mutuality:mutual-language-speaking;

	person sub entity,
		plays speaking-of-language:speaker,
		plays speaking-language-mutuality:mutual-speaker;

	##################################
	## speaking languages in common ##
	##################################

	speaking-language-mutuality sub relation,
		relates mutual-speaker,
		relates mutual-language-speaking,
		relates mutual-language;

	rule people-speak-the-same-language:
		when {
			$sol1 (speaker: $p1, spoken: $l) isa speaking-of-language;
			$sol2 (speaker: $p2, spoken: $l) isa speaking-of-language;
			$p1 != $p2;
		} then {
			(mutual-speaker: $p1, mutual-speaker: $p2, mutual-language: $l, mutual-language-speaking: $sol1, mutual-language-speaking: $sol2) isa speaking-language-mutuality;
		};

#########################
## RELATIONSHIP STATUS ##
#########################

## query the schema section
# match
# 	$ror sub romantic-relationship;
# 	$mar sub marriage;
# 	$per sub person;
# get;

	romantic-relationship sub relation,
		owns start-date,
		owns end-date,
		owns engaged,
		owns engagement-date,
		relates partner;

	open-relationship sub romantic-relationship;

	domestic-relationship sub romantic-relationship;

	complicated-relationship sub romantic-relationship;

	marriage sub relation,
		owns start-date,
		owns end-date,
		owns separated,
		owns divorced,
		relates husband,
		relates wife,
		relates spouse;

	person sub entity,
		plays romantic-relationship:partner,
		plays marriage:spouse,
		plays marriage:wife,
		plays marriage:husband;

################
## FRIENDSHIP ##
################

## query the schema section
# match
# 	$fri sub friendship;
# 	$frr sub friend-request;
# 	$frl sub friends-list;
# 	$per sub person;
# 	$muf sub mutual-friendship;
# get;

	friendship sub relation,
		relates friend,
		plays friend-request:approved-friendship,
		plays friends-list:listed-friendship;

	friend-request sub request,
		owns approved-date,
		relates approved-friendship as approved-subject,
		relates friendship-requester as requester,
		relates friendship-respondent as respondent;

	friends-list sub relation,
		owns title,
		relates list-owner,
		relates listed-friendship;

	person sub entity,
		plays friendship:friend,
		plays friend-request:friendship-requester,
		plays friend-request:friendship-respondent,
		plays friends-list:list-owner,
		plays mutual-friendship:mutual-friend,
		plays mutual-friendship:one-degree-friend;

	#######################
	## mutual friendship ##
	#######################

	mutual-friendship sub relation,
		relates mutual-friend,
		relates one-degree-friend;

	rule people-have-mutual-friends:
		when {
			($p1, $p2) isa friendship;
			($p2, $p3) isa friendship;
		} then {
			(one-degree-friend: $p1, one-degree-friend: $p3, mutual-friend: $p2) isa mutual-friendship;
		};

###########
## GROUP ##
###########

## query the schema section
# match
# 	$gro sub group-ownership;
# 	$grm sub group-membership;
# 	$gmr sub group-membership-request;
# 	$pug sub public-group;
# 	$clg sub closed-group;
# 	$seg sub secret-group;
# 	$per sub person;
# get;

	group-ownership sub ownership,
		relates group-owner as owner,
		relates owned-group as owned;

	group-membership sub relation,
		owns approved-date,
		plays group-membership-request:approved-group-membership,
		relates group-member,
		relates membership-grouping;

	group-membership-request sub request,
		owns approved-date,
		relates approved-group-membership as approved-subject,
		relates group-membership-requester as requester,
		relates group-membership-respondent as respondent;

	social-group sub entity,
		abstract,
		owns name @key,
		plays group-ownership:owned-group,
		plays group-membership:membership-grouping,
		plays sharing:shared-in;

	public-group sub social-group;

	closed-group sub social-group;

	secret-group sub social-group;

	person sub entity,
		plays group-ownership:group-owner,
		plays group-membership:group-member,
		plays group-membership-request:group-membership-requester,
		plays group-membership-request:group-membership-respondent;

	#####################################
	## public group membership request ##
	#####################################

	rule public-group-membership-is-automatically-approved:
		when {
			$group isa public-group;
			(group-owner: $owner, owned-group: $group) isa group-ownership;
			$membership (group-member: $member, membership-grouping: $group) isa group-membership;
		} then {
			(approved-group-membership: $membership, group-membership-requester: $member, group-membership-respondent: $owner) isa group-membership-request;
		};

	###################################
	## group owner is always member ##
	###################################

	rule group-owner-is-always-member:
		when {
			$group isa social-group;
			(group-owner: $owner, owned-group: $group) isa group-ownership;
		} then {
			(group-member: $owner, membership-grouping: $group) isa group-membership;
		};

##############
## TIMELINE ##
##############

## query the schema section
# match
# 	$tow sub timeline-ownership;
# 	$tim sub timeline;
# 	$per sub person;
# get;

	timeline-ownership sub ownership,
		relates timeline-owner as owner,
		relates owned-timeline as owned;

	timeline sub entity,
	    owns identifier @key,
		plays timeline-ownership:owned-timeline,
		plays sharing:shared-in;

	person sub entity,
		plays timeline-ownership:timeline-owner;

##########
## POST ##
##########

## query the schema section
# match
# 	$rep sub reply;
# 	$tag sub tagging;
# 	$att sub attachment;
# 	$rea sub reaction;
# 	$emo sub emotion;
# 	$lin sub link;
# 	$stu sub status-update;
# 	$com sub comment;
# 	$mow sub media-ownership;
# 	$alb sub album;
# 	$vid sub video;
# 	$pho sub photo;
# 	$per sub person;
# get;

	reply sub relation,
		relates replied-to,
		relates reply-content,
		relates replied-by;

	tagging sub relation,
		relates tagged,
		relates tagged-in;

	attachment sub relation,
		relates attached,
		relates attached-to;

	reaction sub relation,
		relates reacted-emotion,
		relates reacted-to,
		relates reacted-by;

	post sub entity,
		abstract,
		owns identifier @key,
		plays permitted-to-see:permitted-content,
		plays sharing:shared-content,
		plays reply:replied-to,
		plays tagging:tagged-in,
		plays reaction:reacted-to;

	status-update sub post,
		owns content,
		plays attachment:attached-to;

	comment sub post,
		owns content,
		plays reply:reply-content,
		plays attachment:attached-to;

	media-ownership sub ownership,
		relates media-owner as owner,
		relates owned-media as owned;

	album sub post,
		owns title,
		owns published-date,
		plays media-ownership:media-owner;

	media sub post,
		abstract,
		owns caption,
		owns file,
		plays media-ownership:owned-media,
		plays attachment:attached;

	video sub media;

	photo sub media;

	person sub entity,
		plays tagging:tagged,
		plays reaction:reacted-by,
		plays reply:replied-by;

#############
## SHARING ##
#############

## query the schema section
# match
# 	$pus sub public-sharing;
# 	$frs sub friends-sharing;
# 	$ins sub inclusive-sharing;
# 	$fes sub friends-with-exclusion-sharing;
# 	$prs sub private-sharing;
# 	$pos sub post;
# 	$pts sub permitted-to-see;
# 	$per sub person;
# 	$puu sub public-user;
# get;

	sharing sub relation,
#       FIXME(vmax): figure this out
#		abstract,
		relates shared-content,
		relates shared-by,
		relates shared-in;

	public-sharing sub sharing;

	friends-sharing sub sharing;

	inclusive-sharing sub sharing,
		relates shared-with;

	friends-with-exclusion-sharing sub sharing,
		relates hidden-from;

	private-sharing sub sharing;

	permitted-to-see sub relation,
		relates permitted-content,
		relates permission-grantee;

	person sub entity,
		plays sharing:shared-by,
		plays inclusive-sharing:shared-with,
		plays friends-with-exclusion-sharing:hidden-from,
		plays permitted-to-see:permission-grantee;

	public-user sub entity,
		plays permitted-to-see:permission-grantee;

	###########################
	## posts view permission ##
	###########################

	rule public-permission:
		when {
			(shared-content: $sc) isa public-sharing;
			$pu isa public-user;
		} then {
			(permitted-content: $sc, permission-grantee: $pu) isa permitted-to-see;
		};

	rule friends-permission:
		when {
			(shared-content: $sc, shared-by: $sb) isa friends-sharing;
			(friend: $sb, $f) isa friendship;
		} then {
			(permitted-content: $sc, permission-grantee: $f) isa permitted-to-see;
		};

	rule inclusive-permissions:
		when {
			(shared-content: $sc, shared-with: $sw) isa inclusive-sharing;
		} then {
			(permitted-content: $sc, permission-grantee: $sw) isa permitted-to-see;
		};

	rule friends-excluded-permission:
		when {
			(shared-content: $sc, shared-by: $sb, hidden-from: $hf) isa friends-with-exclusion-sharing;
			(friend: $sb, $f) isa friendship;
			$f != $hf;
		} then {
			(permitted-content: $sc, permission-grantee: $f) isa permitted-to-see;
		};

	rule private-permission:
		when {
			(shared-content: $sc, shared-by: $sb) isa private-sharing;
			$pu isa public-user;
		} then {
			(permitted-content: $sc, permission-grantee: $sb) isa permitted-to-see;
		};

	rule author-permission:
		when {
			(shared-content: $sc, shared-by: $sb) isa sharing;
			$pu isa public-user;
		} then {
			(permitted-content: $sc, permission-grantee: $sb) isa permitted-to-see;
		};
